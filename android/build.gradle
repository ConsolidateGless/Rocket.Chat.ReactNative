// Top-level build file where you can add configuration options common to all sub-projects/modules.

// Versioning, see https://medium.com/@andr3wjack/versioning-react-native-apps-407469707661
import groovy.json.JsonSlurper
def getNpmVersion() {
    def inputFile = new File("../package.json")
    def packageJson = new JsonSlurper().parseText(inputFile.text)
    return packageJson["version"]
}

def getNpmVersionArray() { // major [0], minor [1], patch [2]
    def (major, minor, patch) = getNpmVersion().tokenize('.')
    return [Integer.parseInt(major), Integer.parseInt(minor), Integer.parseInt(patch)] as int[]
}

buildscript {
    ext {
        buildToolsVersion = "28.0.3"
        minSdkVersion = 21
        compileSdkVersion = 28
        targetSdkVersion = 28
        glideVersion = "4.9.0"
        kotlin_version = "1.3.50"
        supportLibVersion = "28.0.0"
    }
    repositories {
        mavenLocal()
        google()
        jcenter()
        maven {
            url 'https://maven.fabric.io/public'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.2'
        classpath 'com.google.gms:google-services:4.2.0'
        // classpath 'com.google.gms:google-services:4.3.2'
        classpath 'io.fabric.tools:gradle:1.28.1'
        classpath 'com.google.firebase:perf-plugin:1.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.bugsnag:bugsnag-android-gradle-plugin:4.+'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        mavenLocal()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url("$rootDir/../node_modules/react-native/android")
        }
        maven {
            // Android JSC is installed from npm
            url("$rootDir/../node_modules/jsc-android/dist")
        }
        maven {
            // We should change it when Jitsi-SDK release v2.4
            url("$rootDir/../node_modules/react-native-jitsi-meet/jitsi-sdk")
            // url "https://github.com/jitsi/jitsi-maven-repository/raw/master/releases"
        }

        google()
        jcenter()

        maven { url 'https://maven.google.com' }
        maven { url "https://jitpack.io" }
    }
}

subprojects { subproject ->
    ext {
        def npmVersion = getNpmVersionArray()
        versionMajor = npmVersion[0]
        versionMinor = npmVersion[1]
        versionPatch = npmVersion[2]
    }

    afterEvaluate {
        if ((subproject.plugins.hasPlugin('android') || subproject.plugins.hasPlugin('android-library'))) {
            android {
                compileSdkVersion 28
                buildToolsVersion "28.0.3"
                defaultConfig {
                    targetSdkVersion 28
                }
            }
        }
    }
}
